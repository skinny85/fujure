entrypoints FileContents ;

FileInNamedPackage   . FileContents ::= "package" PkgName Imports Defs ;
FileInDefaultPackage . FileContents ::= Imports Defs ;

PackageName . PkgName ::= [PkgFragm] ;
separator nonempty PkgFragm "." ;
PackageFragment . PkgFragm ::= JID ;

ImportStmts . Imports ::= [Import] ;
separator Import "" ;
ImportStmt . Import ::= "import" [ImportFragm] ;
separator nonempty ImportFragm "." ;
ImportFragment . ImportFragm ::= JID ;

Definitions . Defs ::= [Def] ;
separator Def "" ;
SimpleValueDef . Def ::= "def" Binding ;

OnlyNameBinding . Binding ::= JID ;
NameTypeBinding . Binding ::= JID ":" TypeSpec ;
NameInitBinding . Binding ::= JID "=" Expr ;
FullBinding     . Binding ::= JID ":" TypeSpec "=" Expr ;

TypeSpecifier . TypeSpec ::= [TypeSpecFragm] ;
separator nonempty TypeSpecFragm "." ;
TypeSpecFragment . TypeSpecFragm ::= JID ;

OrExpr             . Expr   ::= Expr "||" Expr1 ;
AndExpr            . Expr1  ::= Expr1 "&&" Expr2 ;
EqualityExpr       . Expr2  ::= Expr2 "==" Expr3 ;
InequalityExpr     . Expr2  ::= Expr2 "!=" Expr3 ;
LesserExpr         . Expr3  ::= Expr3 "<" Expr4 ;
LesserEqualExpr    . Expr3  ::= Expr3 "<=" Expr4 ;
GreaterExpr        . Expr3  ::= Expr3 ">" Expr4 ;
GreaterEqualExpr   . Expr3  ::= Expr3 ">=" Expr4 ;
AdditionExpr       . Expr4  ::= Expr4 "+" Expr5 ;
SubtractionExpr    . Expr4  ::= Expr4 "-" Expr5 ;
MultiplicationExpr . Expr5  ::= Expr5 "*" Expr6 ;
DivisionExpr       . Expr5  ::= Expr5 "/" Expr6 ;
ModuloExpr     	   . Expr5  ::= Expr5 "%" Expr6 ;
NotExpr            . Expr6  ::= "!" Expr6 ;
VariableExpr       . Expr6  ::= ValRef ;
LiteralExpr        . Expr6  ::= Literal ;
coercions Expr 6 ;

ValueRef . ValRef ::= [ValRefFragm] ;
separator nonempty ValRefFragm "." ;
ValueRefFragment . ValRefFragm ::= JID ;

IntLiteral        . Literal ::= Integer ;
UnitLiteral       . Literal ::= "unit" ;
BoolTrueLiteral   . Literal ::= "true" ;
BoolFalseLiteral  . Literal ::= "false" ;
CharLiteral       . Literal ::= JCHAR ;
StringLiteral     . Literal ::= JSTRING ;

token JID ('_' | '$' | letter) ('_' | '$' | letter | digit)* ;

token JCHAR '\'' ((char - ["'\\"]) | ('\\' ["'\\nt"])) '\'' ;

token JSTRING '"' ((char - ["\"\\"]) | ('\\' ["\"\\nt"]))* '"' ;

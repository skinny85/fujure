allprojects {
    group 'org.fujure'
    version '0.0.1'
}

subprojects {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'http://dl.bintray.com/skinny85/maven-repo'
        }
    }

    apply plugin: 'java'
    apply plugin: 'maven-publish'

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }

        repositories {
            maven {
                url "${rootProject.projectDir}/repo"
            }
        }
    }
}

def kotlin_subprojects = ['fbc-core', 'fbc-cli']

configure(subprojects.findAll { subproject ->
        kotlin_subprojects.any { subproject.name.contains(it) }}) {
    buildscript {
        ext.kotlin_version = '1.1.3-2'

        repositories {
            mavenCentral()
        }

        dependencies {
            classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        }
    }

    apply plugin: 'kotlin'

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

        testCompile 'junit:junit:4.12'
        testCompile 'org.specnaz:specnaz-kotlin-junit:1.1'
        testCompile group: 'org.assertj', name: 'assertj-core', version: '3.8.0'
        testCompile group: 'org.mockito', name: 'mockito-core', version: '2.8.47'
        testCompile "com.nhaarman:mockito-kotlin-kt1.1:1.5.0"
    }
}

buildscript {
    ext.kotlin_version = '1.1.3-2'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
